apply plugin: 'com.android.application'
apply plugin: 'com.google.firebase.firebase-crash'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'jacoco-android'

def applicationName = "DmsExplorer"
def versionMajor = 0
def versionMinor = 7
def versionPatch = 26

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.1"

    defaultConfig {
        applicationId "net.mm2d.dmsexplorer"
        minSdkVersion 16
        targetSdkVersion 26
        versionCode versionMajor * 10000 + versionMinor * 100 + versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        resConfigs "en", "ja"
        vectorDrawables.useSupportLibrary = true
        buildConfigField "long", "BUILD_TIME", "${System.currentTimeMillis()}L"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dataBinding {
        enabled = true
    }
    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix = ".debug"
            versionNameSuffix = "d"
            manifestPlaceholders = [app_name: "Dubug"]
        }
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [app_name: "@string/app_name"]
        }
    }
    applicationVariants.all { variant ->
        if (variant.buildType.name != "release") {
            return
        }
        variant.outputs.each { output ->
            if (output.outputFile != null && output.outputFile.name.endsWith('release.apk')) {
                def name = "${applicationName}-${versionName}.apk"
                output.outputFile = new File(output.outputFile.parent, name)
            }
        }
    }
    lintOptions {
        abortOnError false
    }
    testOptions {
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
        }
    }
}

repositories {
    maven {
        url 'https://maven.google.com'
    }
    maven {
        url 'https://ohmae.github.com/mmupnp/maven'
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:26.0.1'
    compile 'com.android.support:cardview-v7:26.0.1'
    compile 'com.android.support:customtabs:26.0.1'
    compile 'com.android.support:design:26.0.1'
    compile 'com.android.support:palette-v7:26.0.1'
    compile 'com.android.support:recyclerview-v7:26.0.1'
    compile 'com.android.support:support-v4:26.0.1'
    compile 'com.google.firebase:firebase-core:11.2.0'
    compile 'com.google.firebase:firebase-crash:11.2.0'
    compile 'net.mm2d.mmupnp:mmupnp:1.2.2'
    compile 'net.opacapp:multiline-collapsingtoolbar:1.5.0'
    compile 'com.squareup.okhttp3:okhttp:3.8.1'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'io.reactivex.rxjava2:rxjava:2.1.3'
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'org.mockito:mockito-core:2.9.0'
    testCompile 'org.robolectric:robolectric:3.4.2'
}

jacocoAndroidUnitTestReport {
    html.enabled false
    xml.enabled true
}

setProperty("FirebaseServiceAccountFilePath",
        rootProject.rootDir.parentFile.absolutePath + "/firebase-crashreporting-dmsexplorer.json")

apply plugin: 'com.google.gms.google-services'
